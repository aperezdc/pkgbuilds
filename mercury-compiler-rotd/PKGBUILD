# Maintainer: Bart Verhoeven <nepherte at archlinux dot us>

pkgname=mercury-compiler-rotd
pkgver=20150202
_rotd=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
pkgrel=1
pkgdesc="daily snapshot of the mercury language compiler"
arch=('i686' 'x86_64')
url="http://www.mercury.csse.unimelb.edu.au/index.html"
license=('GPL')
depends=('gcc' 'make')
makedepends=('flex' 'bison')
provides=(mercury-compiler)
conflicts=(mercury-compiler)
changelog=$pkgname.changelog
source=("http://dl.mercurylang.org/rotd/mercury-srcdist-rotd-${_rotd}.tar.gz")
md5sums=('SKIP')
options=('staticlibs')

build() {
  cd "${srcdir}/mercury-srcdist-rotd-${_rotd}"

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info --disable-debug-grades \
              --disable-inefficient-grades

  # Uncomment PARALLEL and change -j accordingly to speed  up compilation
  make PARALLEL="-j1"
}

package() {
  cd "${srcdir}/mercury-srcdist-rotd-${_rotd}"

  # Uncomment PARALLEL and change -j accordingly to speed  up compilation
  make INSTALL_PREFIX="$pkgdir/usr" INSTALL_MAN_DIR="$pkgdir/usr/share/man" \
       INSTALL_INFO_DIR="$pkgdir/usr/share/info" \
       MERCURY_COMPILER="$pkgdir/usr/bin/mercury_compile" \
       MERCURY_CONFIG_DIR="$pkgdir/usr/lib/mercury" \
       install
}

# vim:set ts=2 sw=2 et:
