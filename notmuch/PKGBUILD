# Maintainer: fauno <fauno at kiwwwi.com.ar>
# Maintainer: Olivier Ramonat <olivier at ramonat dot fr>
# Maintainer: Richard Murri <admin@richardmurri.com>
# Maintainer: Adrian Perez <aperez@igalia.com>

pkgname=notmuch
pkgver=0.10
pkgrel=1
pkgdesc="Notmuch is not much of an email program"
arch=(i686 x86_64 mips64el)
url="http://notmuchmail.org/"
license=('GPL3')
depends=('xapian-core' 'gmime')
makedepends=('talloc' 'python2' 'emacs' 'gnupg')
optdepends=('emacs: for using the emacs interface'
            'vim: for using the vim interface'
            'python2: for using the python bindings'
            'gnupg: for email encryption')
source=("http://notmuchmail.org/releases/${pkgname}-${pkgver}.tar.gz")
sha1sums=('6dc3d84da53cb16818d06e1825022f3b23e473d5')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make test
}

package(){
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 install

  install -D notmuch $pkgdir/usr/sbin/notmuch

  mkdir -p $pkgdir/usr/share/vim/vimfiles/{plugin,syntax}

  cd vim && make PREFIX="$pkgdir/usr/share/vim/vimfiles" install

  # Install python bindings

  cd "$srcdir/${pkgname}-${pkgver}/bindings/python"

  find "." -name '*.py' -print0 |xargs -0 \
      sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
      -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
#  ln -s ../../lib/libnotmuch.so.1 libnotmuch.so.1
  env LD_LIBRARY_PATH="." python2 setup.py build
  env LD_LIBRARY_PATH="." python2 setup.py install --prefix=/usr --root=$pkgdir
}
