pkgname='ostree-git'
pkgver=2014.4.r17.g7ccfc93
pkgrel=1
url="http://git.gnome.org/browse/ostree/tree/README.md"
pkgdesc='OSTree is a tool for managing bootable, versioned filesystem trees.'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
provides=('ostree')
makedepends=('pkg-config' 'gtk-doc')
depends=('libarchive>=2.8'
         'linux-user-chroot'
         'libsoup>=2.39.1'
         'gpgme'
         'systemd'
         'libgsystem>=2014.2'
         'xz>=5.1.1'
         'gobject-introspection>=1.34')

source=("${pkgname}::git+git://git.gnome.org/ostree")
md5sums=('SKIP')

pkgver () {
  cd "${srcdir}/${pkgname}"
	git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname}"
	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--with-soup \
		--with-gpgme \
		--with-systemd \
		--with-libarchive \
		--with-mkinitcpio \
		--without-dracut \
		--without-selinux \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \
		--enable-gtk-doc
	make
}

package() {
  cd "${srcdir}/${pkgname}"
  make install DESTDIR="$pkgdir"
}


