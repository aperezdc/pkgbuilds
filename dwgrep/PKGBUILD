# Maintainer: Adrián Pérez de Castro <aperez@igalia.com>
pkgname='dwgrep'
pkgver='0.1'
pkgrel='1'
pkgdesc='Tool and library for querying Dwarf (debuginfo) graphs.'
url='http://pmachata.github.io/dwgrep/'
arch=('i686' 'x86_64')
makedepends=('cmake' 'flex' 'bison' 'python-sphinx')
license=('LGPL')
depends=('elfutils')
source=("https://github.com/pmachata/dwgrep/archive/${pkgver}.tar.gz")
sha512sums=('116b7ba3d866661af0e0d802cc5013d60c90d1588546991c719bb2b4fd225937f8428f48a901f76843cc5aae7e8fa224e01e91aba3db51b4c4213dbb416b512c')

build () {
	cd "${pkgname}-${pkgver}"
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
	make doc
}

package () {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Fix library directory name in 64-bit targets.
	if [[ -d ${pkgdir}/usr/lib64 ]] ; then
		mv "${pkgdir}/usr/lib64" "${pkgdir}/usr/lib"
	fi
}
