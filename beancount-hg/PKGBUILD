# Maintainer: Adrian Perez <aperez@igalia.com>
pkgname='beancount-hg'
pkgdesc='Double-Entry Accounting from Text Input'
pkgver=r1032.b54133813083
pkgrel=1
arch=('any')
url="http://furius.ca/beancount/"
license=('GPL')
source=("${pkgname}::hg+https://hg.furius.ca/public/beancount/"
	fix-setup.py.patch)
sha1sums=('SKIP'
          '95ace84cf923cd3fd0e234944ee1d4ec79a5a494')

export HGOPTIONS=--insecure

# Hack to make sure we pass --insecure to Mercurial when cloning/pulling
hg () {
	if [[ $1 == clone ]] ; then
		shift
		command hg clone --insecure "$@"
	elif [[ $1 == pull ]] ; then
		shift
		command hg pull --insecure "$@"
	else
		command hg "$@"
	fi
}

# TODO: Add python-cdecimal when package is available
depends=(
	python-bottle
	wget
)

pkgver () {
	cd "${pkgname}"
	printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

prepare () {
	cd "${pkgname}"
	patch -p0 < "${srcdir}/fix-setup.py.patch"
}

build () {
	cd "${pkgname}"
	export CFLAGS='-std=gnu99'
	python3 setup.py build_ext -i
	python3 setup.py build
}

package () {
	cd "${pkgname}"
	python3 setup.py install --prefix=/usr --root="${pkgdir}"
}
